apply plugin: 'com.android.application'
apply plugin: 'com.novoda.build-properties'

buildProperties {
    getPublic {
        file rootProject.file('public.properties')
    }
    getLocal {
        file rootProject.file('local.properties')
    }
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "com.thirtydegreesray.openhub"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 30
        versionName "3.0.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled false

        buildConfigField "String", "OPENHUB_CLIENT_ID", (buildProperties.getLocal['openhub_client_id'] | buildProperties.getPublic['openhub_client_id']).string
        buildConfigField "String", "OPENHUB_CLIENT_SECRET", (buildProperties.getLocal['openhub_client_secret'] | buildProperties.getPublic['openhub_client_secret']).string
        buildConfigField "String", "BUGLY_ID", (buildProperties.getLocal['bugly_appid'] | buildProperties.getPublic['bugly_appid']).string
        buildConfigField "String", "DEBUG_BUGLY_ID", (buildProperties.getLocal['debug_bugly_appid'] | buildProperties.getPublic['debug_bugly_appid']).string

        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
    }

    signingConfigs {
        release {
            storeFile(file((buildProperties.getLocal['sign_path'] | buildProperties.getPublic['sign_path']).string))
            storePassword((buildProperties.getLocal['store_password'] | buildProperties.getPublic['store_password']).string)
            keyAlias((buildProperties.getLocal['key_alias'] | buildProperties.getPublic['key_alias']).string)
            keyPassword((buildProperties.getLocal['key_password'] | buildProperties.getPublic['key_password']).string)
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        encoding = 'UTF-8'
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        checkReleaseBuilds true
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    flavorDimensions "default"

    productFlavors {
        normal {
            dimension "default"
            // add app_name int string.xml
            resValue "string", "app_name", "OpenHub"
        }
        fullName {
            dimension "default"
            // add app_name int string.xml
            resValue "string", "app_name", "OpenHub for GitHub"
        }
    }

    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [BUGLY_APP_CHANNEL_VALUE: name]
    }
}

ext {
    ANDROID_SUPPORT_VERSION = "28.0.0"
    BUTTERKNIFE_VERSION = "8.7.0" //8.8.1
    DAGGER_VERSION = "2.14.1" //2.19
    DATAAUTOACCESS_VERSION = "1.2.8"
    GLIDE_VERSION = "4.0.0" //4.8.0
    MATERIAL_DIALOGS_VERSION = "0.9.6.0"
    RETROFIT2_VERSION = "2.1.0" //2.5.0
//    OKHTTP3_VERSION = "3.12.0"
//    FASTJSON_VERSION = "1.1.70.android" //1.2.54
//    FILE_DOWNLOADER_VERSION = "0.3.1"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'

    implementation "com.android.support:customtabs:${ANDROID_SUPPORT_VERSION}"
    implementation "com.android.support:design:${ANDROID_SUPPORT_VERSION}"
    implementation "com.android.support:support-v4:${ANDROID_SUPPORT_VERSION}"
    implementation "com.android.support:appcompat-v7:${ANDROID_SUPPORT_VERSION}"
    implementation "com.android.support:cardview-v7:${ANDROID_SUPPORT_VERSION}"
    implementation "com.android.support:preference-v7:${ANDROID_SUPPORT_VERSION}"
    implementation "com.android.support:recyclerview-v7:${ANDROID_SUPPORT_VERSION}"
//    implementation "com.android.support:support-v13:${ANDROID_SUPPORT_VERSION}"
    implementation "com.android.support:preference-v14:${ANDROID_SUPPORT_VERSION}"
    implementation "com.android.support:support-vector-drawable:${ANDROID_SUPPORT_VERSION}"
//    implementation "com.android.support:support-annotations:${ANDROID_SUPPORT_VERSION}"

    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
//    implementation 'com.android.support:multidex:1.0.3'

    implementation "com.thirtydegreesray:dataautoaccess:${DATAAUTOACCESS_VERSION}"
    annotationProcessor "com.thirtydegreesray:dataautoaccess-compiler:${DATAAUTOACCESS_VERSION}"

    implementation "com.jakewharton:butterknife:${BUTTERKNIFE_VERSION}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${BUTTERKNIFE_VERSION}"

    //Database tool
    implementation "org.greenrobot:greendao:${GREENDAO_VERSION}"
    //EventBus
    implementation 'org.greenrobot:eventbus:3.1.1'

    //RxJava
    implementation 'io.reactivex:rxjava:1.1.0' //1.3.8
//    implementation 'io.reactivex.rxjava2:rxjava:2.2.4'
    implementation 'io.reactivex:rxandroid:1.1.0' //1.2.1
//    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'

    //Type-safe HTTP client for Android and Java by Square, Inc.
    implementation "com.squareup.retrofit2:retrofit:${RETROFIT2_VERSION}"
    implementation "com.squareup.retrofit2:converter-gson:${RETROFIT2_VERSION}"
    implementation "com.squareup.retrofit2:adapter-rxjava:${RETROFIT2_VERSION}"
    implementation("com.squareup.retrofit2:converter-simplexml:${RETROFIT2_VERSION}", {
        exclude group: 'xpp3', module: 'xpp3'
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'stax', module: 'stax'
    })

    //Dagger (dependency injection)
    implementation "com.google.dagger:dagger:${DAGGER_VERSION}"
    annotationProcessor "com.google.dagger:dagger-compiler:${DAGGER_VERSION}"

    implementation 'com.tbruyelle.rxpermissions:rxpermissions:0.9.4@aar'
//    implementation 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.5@aar'

    implementation "com.github.bumptech.glide:glide:${GLIDE_VERSION}"
    annotationProcessor "com.github.bumptech.glide:compiler:${GLIDE_VERSION}"

    implementation "com.afollestad.material-dialogs:core:${MATERIAL_DIALOGS_VERSION}"
    implementation "com.afollestad.material-dialogs:commons:${MATERIAL_DIALOGS_VERSION}"

    implementation 'de.hdodenhof:circleimageview:2.2.0'
//    implementation 'com.makeramen:roundedimageview:2.3.0'

    implementation 'com.github.GrenderG:Toasty:1.3.0'
    implementation 'com.github.daniel-stoneuk:material-about-library:2.2.3' //2.2.4, 2.4.1, 2.4.2
    implementation 'com.unstoppable:submitbutton:1.1.3'

    implementation(name: 'bugly_crashreport_upgrade-1.3.6', ext: 'aar')
    implementation 'com.tencent.bugly:nativecrashreport:3.3.1'
    implementation 'com.zzhoujay.richtext:richtext:2.5.4' //2.6.0, 3.0.7
    implementation 'com.zzhoujay.glideimagegetter:glideimagegetter:1.0.5'

    implementation 'com.orhanobut:logger:2.2.0'
    implementation 'com.timehop.stickyheadersrecyclerview:library:0.4.3'

    implementation 'org.jsoup:jsoup:1.11.3'
}
